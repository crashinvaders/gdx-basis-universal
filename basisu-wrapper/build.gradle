apply plugin: 'java-library'

group 'com.crashinvaders.gdx-basis-universal'
version '0.1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava.options.incremental = true
compileJava.options.headerOutputDirectory.set(file('jni'))

sourceSets.main.java.srcDirs += ['src']
sourceSets.test.java.srcDirs += ['test']
sourceSets.test.resources.srcDirs += ['test-resources']

dependencies {
    testRuntimeOnly files('libs/gdx-basis-universal-natives.jar')
    testImplementation "junit:junit:$junitVersion"
    testImplementation "com.badlogicgames.gdx:gdx-jnigen-loader:$jnigenVersion"
}

test {
    useJUnit()
    maxHeapSize = '1G'
}

//region jnigen configuration
apply plugin: 'com.badlogicgames.gdx.gdx-jnigen'

def nativeSrcFiles = [
        'com_crashinvaders_basisu_wrapper_BasisuWrapper.cpp',
        'com_crashinvaders_basisu_wrapper_BasisuFileInfo.cpp',
        'com_crashinvaders_basisu_wrapper_BasisuImageInfo.cpp',
//        'src/basisu_transcoder.cpp',
        'src/basisu_wrapper.cpp',
        'src/basisu_native_utils.cpp',
        'contrib/basis_universal/transcoder/basisu_transcoder.cpp',
]

jnigen {
    sharedLibName = 'gdx-basis-universal'

    nativeCodeGenerator {
        sourceDir = '/src'
    }

    all {
        headerDirs = ['include', 'contrib/basis_universal/transcoder']
        cExcludes = cppExcludes = ['build/**',]

        cppIncludes = nativeSrcFiles

        cFlags +=      ' -std=c11   -fvisibility=hidden -Wall -O2 -g0 -flto -fno-exceptions '
        cppFlags +=    ' -std=c++11 -fvisibility=hidden -Wall -O2 -g0 -flto -fno-exceptions '
        linkerFlags += '            -fvisibility=hidden -Wall -O2 -g0 -flto -fno-exceptions '

        // Enable for Basis Universal debug messages.
        //  cppFlags += ' -DBASISU_FORCE_DEVEL_MESSAGES=1 '
    }
    add(Windows, x32) {
        compilerPrefix = 'mingw32-'
        compilerSuffix = '.exe'
    }
    add(Windows, x64) {
        compilerSuffix = '.exe'
    }
    add(Linux, x32)
    add(Linux, x64)
    add(Linux, x32, ARM)
    add(Linux, x64, ARM)
    add(MacOsX, x64)
    add(Android) {
        linkerFlags += '-llog'
    }
    add(IOS) {
        cppFlags += ' -stdlib=libc++ '
    }
}

getTasksByName('jnigen', false).first().doLast {
    //FIXME Remove this once "jnigen" task creates "libs" under the respected module (currently it writes to the root project dir).
    if (file("${rootDir}/libs").exists()) {
        println "Applying libs dir fix..."
        ant.move file: "${rootDir}/libs",
                todir: "${projectDir}"
    }

    // Update "ndkSuffix" Android build property with platform specific file suffix.
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        ant.replaceregexp(
                match: '<property name="ndkSuffix" value="" />',
                replace: '<property name="ndkSuffix" value=".cmd" />',
                flags: 'g',
                byline: true) {
            fileset(dir: 'jni', includes: 'build-android32.xml')
        }
    }

    // Fix missing source files in Android.mk.
    ant.replaceregexp(
            match: 'LOCAL_SRC_FILES :=  ',
            replace: "LOCAL_SRC_FILES := ${String.join(' ', nativeSrcFiles)}\n",
            flags: 'g',
            byline: true) {
        fileset(dir: 'jni', includes: 'Android.mk')
    }
}

//if (file('jni/build.xml').exists()) {
//    println 'Importing ANT tasks from \"jni/build.xml\"'
//    ant.importBuild('jni/build.xml') { antTargetName ->
//        def taskName = 'jnigen-build-' + antTargetName
//        println "\"$taskName\" ANT task has been imported."
//        taskName
//    }
//}
//endregion