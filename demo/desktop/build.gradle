apply plugin: "java"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava.options.incremental = true

sourceSets.main.java.srcDirs += ['src']
sourceSets.main.resources.srcDirs += ['../assets']

dependencies {
    implementation project(':demo:core')
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    runtimeOnly "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

    if (useGdxBasisuMavenArtifacts) {
        runtimeOnly "com.crashinvaders.basisu:basisu-wrapper:$gdxBasisuVersion:natives-desktop"
    } else {
        def buildDir = project(':basisu-wrapper').layout.buildDirectory.get()
        runtimeOnly files("$buildDir/libs/basisu-wrapper-${gdxBasisuVersion}-natives-desktop.jar")
    }
}

task run(dependsOn: classes, type: JavaExec) {
    main = "com.crashinvaders.basisu.demo.desktop.DesktopLauncher"
    classpath(sourceSets.main.runtimeClasspath)
    classpath(sourceSets.main.resources)
    standardInput = System.in
    ignoreExitValue = true

    if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
        jvmArgs '-XstartOnFirstThread'
    }
}

task fatJar(type: Jar) {
// sets the name of the .jar file this produces to the name of the game or app.
    archiveBaseName.set(appName)
// using 'lib' instead of the default 'libs' appears to be needed by jpackageimage.
    destinationDirectory = file("${project.layout.buildDirectory.asFile.get().absolutePath}/lib")
// the duplicatesStrategy matters starting in Gradle 7.0; this setting works.
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    dependsOn classes, configurations.runtimeClasspath
    from configurations.runtimeClasspath.collect {it.isDirectory() ? it : zipTree(it) } with jar
// these "exclude" lines remove some unnecessary duplicate files in the output JAR.
    exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
    dependencies {
        exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
    }
// setting the manifest makes the JAR runnable.
    manifest {
        attributes 'Main-Class': "com.crashinvaders.basisu.demo.desktop.DesktopLauncher"
    }
// this last step may help on some OSes that need extra instruction to make runnable JARs.
    doLast {
        file(archiveFile).setExecutable(true, false)
    }
}