apply plugin: 'java-library'
apply plugin: "gwt"
apply plugin: "war"
apply plugin: "org.gretty"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

sourceSets.main.java.srcDirs += [ "src" ]
sourceSets.main.resources.srcDirs += ['../assets']

gwt {
    gwtVersion = gwtLibVersion  // Should match the version used for building the GWT backend. See gradle.properties.
    maxHeapSize = "1G"          // Default 256m is not enough for gwt compiler. GWT is HUNGRY
    minHeapSize = "1G"

    src = files(file('src/')) // Needs to be in front of "modules" below.
    modules 'com.crashinvaders.basisu.demo.GdxDefinition'
    devModules 'com.crashinvaders.basisu.demo.GdxDefinitionSuperdev'
    project.webAppDirName = 'webapp'

    compiler.strict = true
    compiler.disableCastChecking = true

//    logLevel = 'DEBUG'
}

dependencies {
    implementation project(':demo:core')
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
    implementation "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"

    if (useGdxBasisuMavenArtifacts) {
        implementation "com.crashinvaders.basisu:basisu-gdx-gwt"
        implementation "com.crashinvaders.basisu:basisu-gdx-gwt:$gdxBasisuVersion:sources"
        implementation "com.crashinvaders.basisu:basisu-gdx:$gdxBasisuVersion:sources"
        implementation "com.crashinvaders.basisu:basisu-wrapper:$gdxBasisuVersion:sources"
        implementation "com.crashinvaders.basisu:basisu-wrapper:$gdxBasisuVersion:natives-web"
    } else {
        implementation project(':basisu-gdx-gwt')
        def buildDir = project(':basisu-wrapper').layout.buildDirectory.get()
        implementation files("$buildDir/libs/basisu-wrapper-${gdxBasisuVersion}-natives-web.jar")
    }
}

gretty.httpPort = 8080
gretty.resourceBase = "${project.layout.buildDirectory.get()}/gwt/draftOut"
gretty.contextPath = "/"
gretty.portPropertiesFileName = "TEMP_PORTS.properties"

task startHttpServer (dependsOn: [draftCompileGwt]) {
    doFirst {
        copy {
            from "webapp"
            into gretty.resourceBase
        }
        copy {
            from "war"
            into gretty.resourceBase
        }
    }
}
task beforeRun(type: org.akhikhl.gretty.AppBeforeIntegrationTestTask, dependsOn: startHttpServer) {
    // The next line allows ports to be reused instead of
    // needing a process to be manually terminated.
    file("build/TEMP_PORTS.properties").delete()
    // Somewhat of a hack; uses Gretty's support for wrapping a task in
    // a start and then stop of a Jetty server that serves files while
    // also running the SuperDev code server.
    integrationTestTask 'superDev'

    interactive false
}

task superDev(type: org.wisepersist.gradle.plugins.gwt.GwtSuperDev) {
    doFirst {
        gwt.modules = gwt.devModules
    }
}

// We delete the (temporary) war/ folder because if any extra files get into it, problems occur.
// The war/ folder shouldn't be committed to version control.
clean.delete += [file("war")]

// This next line can be changed if you want to, for instance, always build into the
// docs/ folder of a Git repo, which can be set to automatically publish on GitHub Pages.
// This is relative to the html/ folder.
var outputPath = "build/dist/"

task dist(dependsOn: [clean, compileGwt]) {
    doLast {
        // Uncomment the next line if you have changed outputPath and know that its contents
        // should be replaced by a new dist build. Some large JS files are not cleaned up by
        // default unless the outputPath is inside build/ (then the clean task removes them).
        // Do not uncomment the next line if you changed outputPath to a folder that has
        // non-generated files that you want to keep!
        //delete(file(outputPath))

        file(outputPath).mkdirs()
        copy {
            from("build/gwt/out"){
                exclude '**/*.symbolMap' // Not used by a dist, and these can be large.
            }
            into outputPath
        }
        copy {
            from("webapp") {
                exclude 'index.html' // We edit this HTML file later.
                exclude 'refresh.png' // We don't need this button; this saves some bytes.
            }
            into outputPath
        }
        copy {
            from("webapp") {
                // These next two lines take the index.html page and remove the superdev refresh button.
                include 'index.html'
                filter { String line -> line.replaceAll('<a class="superdev" .+', '') }
                // This does not modify the original index.html, only the copy in the dist.
                // If you decide to manually remove or comment out the superdev button from index.html, you should also
                // either remove or comment out only the "filter" line above this.
            }
            into outputPath
        }
        copy {
            from "war"
            into outputPath
        }
    }
}

task addSource {
    doLast {
        sourceSets.main.compileClasspath += files(project(':basisu-wrapper').sourceSets.main.allJava.srcDirs)
        sourceSets.main.compileClasspath += files(project(':basisu-gdx').sourceSets.main.allJava.srcDirs)
        sourceSets.main.compileClasspath += files(project(':basisu-gdx-gwt').sourceSets.main.allJava.srcDirs)
        sourceSets.main.compileClasspath += files(project(':demo:core').sourceSets.main.allJava.srcDirs)
    }
}

task distZip(type: Zip, dependsOn: dist){
    // This uses the output of the dist task, which removes the superdev button from index.html .
    from(outputPath)
    archiveBaseName.set("${appName}-dist")
    // The result will be in html/build/ with a name containing "-dist".
    destinationDirectory.set(file("build"))
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)
tasks.checkGwt.dependsOn(addSource)