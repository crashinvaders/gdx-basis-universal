buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://s01.oss.sonatype.org' }
        gradlePluginPortal()
        mavenLocal()
        maven { url 'https://central.sonatype.com/repository/maven-snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    }
    dependencies {
        classpath "com.badlogicgames.gdx:gdx-jnigen-gradle:$jnigenVersion"
    }
}

allprojects {
    apply plugin: "idea"

    repositories {
        google()
        mavenCentral()
        mavenLocal()
        maven { url 'https://s01.oss.sonatype.org' }
        maven { url "https://repo1.maven.org/maven2/" }
        maven { url 'https://central.sonatype.com/repository/maven-snapshots/' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    ext {
        projectGroup = 'com.crashinvaders.basisu'
        versionName = '1.1.0'
        isSnapshot = false
        version = "$versionName${isSnapshot ? '-SNAPSHOT' : ''}"
    }
}

/** Checks if the property exists and if not tries to resolve it from the environment variable. */
void resolveProperty(String propertyName, String envVariableName) {
    if (!project.hasProperty(propertyName)) {
        project.ext.propertyName = System.getenv(envVariableName)
        if (!project.ext.propertyName) {
            println "$propertyName is not defined"
        } else {
            println "$propertyName has been set from an environment variable."
        }
    }
}
// Setup Nexus repo and GitHub package repo credentials.
// They usually defined in user global gradle.properties file
// or as environment variables (Travis-CI).
resolveProperty('sonatypeUsername', 'SONATYPE_USERNAME')
resolveProperty('sonatypePassword', 'SONATYPE_PASSWORD')
resolveProperty('githubUsername', 'GITHUB_USERNAME')
resolveProperty('githubAccessToken', 'GITHUB_ACCESS_TOKEN')

def basisuLibProjects = [ project(':basisu-wrapper'), project(':basisu-gdx'), project(':basisu-gdx-gwt') ]

configure(basisuLibProjects) {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.incremental = true

    version = ext.version
    group = ext.projectGroup

    jar {
        from project.sourceSets.main.output
        from(project.sourceSets.main.allSource) {
            include '**/*.xml'
        }
        archiveBaseName = project.name
        duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    afterEvaluate { project ->
        publishing {
            repositories {
                maven {
                    def urlRelease = 'https://ossrh-staging-api.central.sonatype.com/service/local/'
                    def urlSnapshot = 'https://central.sonatype.com/repository/maven-snapshots/'
                    url = isSnapshot ? urlSnapshot : urlRelease
                    credentials {
                        username sonatypeUsername
                        password sonatypePassword
                    }
                }

//              maven {
//                  name = "MavenLocalExplicit"
//                  url = "file://${System.properties['user.home']}/.m2/repository"
//              }

//              // Read about setting up a personal GitHub access token if you don't have one.
//              // https://docs.github.com/en/free-pro-team@latest/packages/learn-github-packages/about-github-packages#authenticating-to-github-packages
//              repository(url: "https://maven.pkg.github.com/crashinvaders/gdx-basis-universal") {
//                  authentication(userName: project.githubUsername, password: project.githubAccessToken)
//              }
            }
            publications {
                mavenJava(MavenPublication) {

                    artifactId = project.name
                    from components.java

                    // basisu-wrapper has a composite task `jnigenJarNativesAll`
                    // that aggregates the rest of jar native tasks.
                    project.tasks.findByName('jnigenJarNativesAll')?.dependsOn?.forEach { jarNativesTask ->
                        artifact jarNativesTask
                    }

                    pom {
                        name = project.name
                        description = project.description
                        url = POM_URL
                        licenses {
                            license {
                                name = POM_LICENCE_NAME
                                url = POM_LICENCE_URL
                                distribution = POM_LICENCE_DIST
                            }
                        }
                        developers {
                            developer {
                                id = POM_DEV0_ID
                                name = POM_DEV0_NAME
                                email = POM_DEV0_EMAIL
                            }
                        }
                        scm {
                            connection = POM_SCM_CONNECTION
                            developerConnection = POM_SCM_DEV_CONNECTION
                            url = POM_SCM_URL
                        }
                    }
                }
            }
        }

        signing {
            sign publishing.publications.mavenJava
        }

        // Simply using "required" in signing block doesn't work because taskGraph isn't ready yet.
        gradle.taskGraph.whenReady {
            tasks.withType(Sign) {
                onlyIf { !isSnapshot }
            }
        }
    }

    // Invokes publish task if the version is a snapshot.
    task publishSnapshot() {
        onlyIf { isSnapshot }
        dependsOn project.assemble
        dependsOn project.publish
    }
}

task publishAll {
    group 'GDX Basis Universal'
    description 'Publish all the Maven artifacts to the public repos.'

    basisuLibProjects.forEach {project ->
        dependsOn project.build
        dependsOn project.publish
    }
}

task publishAllSnapshots {
    group 'GDX Basis Universal'
    description 'Publish all the Maven artifacts to the public repos.' +
            'This task will only be execute if the current version is a snapshot.'

    basisuLibProjects.forEach {project ->
        dependsOn project.build
        dependsOn project.publishSnapshot
    }
}

task installAll {
    group 'GDX Basis Universal'
    description 'Installes all the Maven artifacts to the local Maven repo.'

    basisuLibProjects.forEach {project ->
        dependsOn project.build
        dependsOn project.publishToMavenLocal
    }
}